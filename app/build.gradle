ext {
    SUPPORT_LIBRARY_VERSION = "27.1.0"
}

apply plugin: 'com.android.application'

repositories {
    google()
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.farmerbb.notepad"
        minSdkVersion 14
        targetSdkVersion 27

        versionCode 99
        versionName "2.2.8"

        resConfigs "en", "fr", "ko", "nl", "pl", "zh-rCN"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'us.feras.mdv:markdownview:1.1.0'
}
